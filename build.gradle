plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group 'xyz.malkki'
version '1.2-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

javadoc {
    exclude '**/internal/**'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.5"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
